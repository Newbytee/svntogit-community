# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kpmcore
pkgver=3.1.1
pkgrel=2
pkgdesc="Library for managing partitions"
arch=(i686 x86_64)
url="http://kde.org/applications/system/kdepartitionmanager/"
license=(GPL2)
depends=(kio parted)
makedepends=(extra-cmake-modules python)
optdepends=('e2fsprogs: ext2/3/4 support'
	    'xfsprogs: XFS support'
	    'jfsutils: JFS support'
	    'reiserfsprogs: Reiser support'
	    'ntfs-3g: NTFS support'
            'dosfstools: FAT32 support'
            'f2fs-tools: F2FS support')
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-381987.patch::"https://cgit.kde.org/kpmcore.git/patch/?id=13c063a1")
sha256sums=('e65c657ab38a0b60b25e3af675ac38ae24bcb1f8bbf2592a11c33309e77ed321'
            'SKIP'
            '1c42378b861c6a6c8fb6bd359caef9152855ca90f7f65d6e785c7cff6cf73d84')
validpgpkeys=(1EE5A3205904BAA2B88C0A9D24FD31940095C0E1) # Andrius Å tikonas <andrius@stikonas.eu>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Fix null pointer dereference https://bugs.kde.org/show_bug.cgi?id=381987
  patch -p1 -i ../kdebug-381987.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
