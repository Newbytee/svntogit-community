# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Ivan Shapovalov <intelfx@intelfx.name>
# Contributor: larte <lauri.arte@gmail.com>
# Contributor: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: gun1x <gunix@gunix.cloud>

pkgname=kubectl
pkgdesc="Kubernetes.io client binary"
pkgver=1.15.1
pkgrel=1
arch=('x86_64')
url="http://kubernetes.io"
license=('Apache')
makedepends=('go-pie' 'rsync')
#source=("git+https://github.com/kubernetes/kubernetes#tag=v${pkgver}")
source=("kubernetes-$pkgver.tar.gz::https://dl.k8s.io/v$pkgver/kubernetes-src.tar.gz")
sha512sums=('52ead4397b3fbcaca927e82ed70bc8a7dae2ce806c60499618a415ca6f697db61e5101a8deafa77dfb80e26e80276ca584c463f53c18436646f33f618a816dc0')

build() {
  make WHAT=cmd/kubectl
  hack/generate-docs.sh
}

package() {
  install -Dm755 _output/bin/kubectl -t "$pkgdir/usr/bin"
  install -Dm644 docs/man/man1/* -t "$pkgdir/usr/share/man/man1"

  "$pkgdir/usr/bin/kubectl" completion bash | install -Dm644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/kubectl"
  "$pkgdir/usr/bin/kubectl" completion zsh | install -Dm644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_kubectl"
}
