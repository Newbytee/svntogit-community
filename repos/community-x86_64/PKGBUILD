# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>
# Contributor: larte <lauri.arte@gmail.com>
# Contributor: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: gun1x <gunix@gunix.cloud>

pkgname=kubectl
pkgdesc="Kubernetes.io client binary"
pkgver=1.17.0
pkgrel=1
arch=('x86_64')
url="https://kubernetes.io"
license=('Apache')
makedepends=('go-pie' 'rsync' 'git')
#source=("git+https://github.com/kubernetes/kubernetes#tag=v${pkgver}")
source=("kubernetes-$pkgver.tar.gz::https://dl.k8s.io/v$pkgver/kubernetes-src.tar.gz")
sha512sums=('5424576d7f7936df15243fee0036e7936d2d6224e98ac805ce96cdf7b83a7c5b66dfffc8823d7bc0c17c700fa3c01841208e8cf89be91d237d12e18f3d2f307c')

build() {
  # this line remains commented, until kubernetes' build system is go 1.13 ready
  #make WHAT=cmd/kubectl
  cd cmd/kubectl
  go build -trimpath -ldflags "-extldflags ${LDFLAGS}" -o kubectl
}

package() {
  install -Dm755 cmd/kubectl/kubectl -t "$pkgdir/usr/bin"
  # man page generation fails with go 1.13.1 right now
  #install -Dm644 docs/man/man1/* -t "$pkgdir/usr/share/man/man1"

  "$pkgdir/usr/bin/kubectl" completion bash | install -Dm644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/kubectl"
  "$pkgdir/usr/bin/kubectl" completion zsh | install -Dm644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_kubectl"
}
