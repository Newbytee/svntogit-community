# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kpmcore
pkgver=2.0.0
pkgrel=1
pkgdesc="Library for managing partitions."
arch=(i686 x86_64)
url="http://kde.org/applications/system/kdepartitionmanager/"
license=(GPL2)
depends=(kio parted)
makedepends=(extra-cmake-modules)
optdepends=('e2fsprogs: ext2/3/4 support'
	    'xfsprogs: XFS support'
	    'jfsutils: JFS support'
	    'reiserfsprogs: Reiser support'
	    'ntfs-3g: NTFS support'
	    'dosfstools: FAT32 support')
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('14570132ef88df21060198fdcc4b8e7a')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
