# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: d'Ronin <daronin@2600.com>

pkgname=botan
pkgver=1.10.3
pkgrel=1
pkgdesc="Crypto library written in C++"
license=('BSD')
arch=('i686' 'x86_64')
url="http://botan.randombit.net/"
depends=('gcc-libs' 'sh')
makedepends=('python2')
source=(http://files.randombit.net/botan/Botan-${pkgver}.tbz{,.asc})
sha1sums=('9f929101bf75c19432f49f57c80d2d26eec91dcb'
          'd03b220849e3d3b3a49363a0debc266b615d9fca')

build() {
  cd "${srcdir}/Botan-${pkgver}"
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' configure.py
  ./configure.py --prefix=/usr
  make 
}

package() {
  cd "${srcdir}/Botan-${pkgver}"
  make DESTDIR="${pkgdir}/usr" install
  install -D -m644 doc/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  find "${pkgdir}/usr/share/doc" -type f -exec chmod 0644 {} \;
}

