# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: gardar <aur@gardar.net>
# Contributor: Morten Linderud <morten@linderud.pw>

_name=testinfra
pkgname=python-testinfra
pkgver=5.3.0
pkgrel=1
pkgdesc='Testinfra test your infrastructures'
url="https://github.com/philpep/testinfra"
arch=('any')
license=('Apache')
depends=('python-pytest')
makedepends=('python-hacking' 'python-setuptools_scm' 'python-sphinx')
checkdepends=('ansible' 'python-mock' 'python-paramiko' 'python-pytest-cov'
'python-pytest-xdist' 'python-pywinrm' 'python-tornado' 'salt')
# TODO: add salt to optdepends, after it has been ported to python3
optdepends=('ansible: for tests using ansible inventories'
            'docker: for test running docker containers'
            'kubectl: for test running containers in kubernetes'
            'lxd: for test running LXC or LXD containers'
            'podman: for test running podman containers'
            'python-paramiko: for testing remote hosts'
            'python-pywinrm: for testing on Windows hosts')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('7bab1952d906e8e5e948e9d1997bfab3c46cafdff04f7d69e16f6cffe8a3b8ea380cc4e4f75c9d9a686a5cb090467730a867a880f41a1e2340032482ad07fb29')
b2sums=('ba9be17e3d31a00bd0b9c0a05401509ab4f2a138703a924fcb748343e99e40be8e1e77b283a97611c556902d994113241a923b3e7b3bd9449b541711debc1db5')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
  make -C doc man
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="build:$PYTHONPATH"
  # salt is not python3 (yet): https://bugs.archlinux.org/task/61129
  pytest -v -k 'not test_backend_importables'
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  # man page
  install -Dm 644 doc/build/man/${_name}.1 \
    "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  # docs
  install -vDm 644 {CHANGELOG,CONTRIBUTING,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
