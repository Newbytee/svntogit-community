# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: gardar <aur@gardar.net>
# Contributor: Morten Linderud <morten@linderud.pw>

_name=testinfra
pkgname=python-testinfra
pkgver=5.2.1
pkgrel=1
pkgdesc='Testinfra test your infrastructures'
url="https://github.com/philpep/testinfra"
arch=('any')
license=('Apache')
depends=('python-pytest')
makedepends=('python-setuptools_scm' 'python-hacking' 'python-sphinx')
checkdepends=('python-mock' 'python-paramiko' 'python-pytest-cov'
'python-pytest-xdist' 'python-pywinrm' 'python-tornado' 'ansible' 'salt')
# TODO: add salt to optdepends, after it has been ported to python3
optdepends=('ansible: for tests using ansible inventories'
            'docker: for test running docker containers'
            'kubectl: for test running containers in kubernetes'
            'lxd: for test running LXC or LXD containers'
            'podman: for test running podman containers'
            'python-paramiko: for testing remote hosts'
            'python-pywinrm: for testing on Windows hosts')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('d5eaac6616d48d033a44ec18026ba34a33185b8f219a0b07cd7023b583c5e67fa8486756fc4f1ecfde16f119be533c757a70252c305399a0337626fcf8650461')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
  make -C doc man
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="build:$PYTHONPATH"
  # salt is not python3 (yet): https://bugs.archlinux.org/task/61129
  pytest -v -k 'not test_backend_importables'
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  # man page
  install -Dm 644 doc/build/man/${_name}.1 \
    "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  # docs
  install -vDm 644 {CHANGELOG,CONTRIBUTING,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
