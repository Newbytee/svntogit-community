# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com

pkgbase=python-sphinxcontrib-websupport
pkgname=(python-sphinxcontrib-websupport python2-sphinxcontrib-websupport)
pkgver=1.0.1
pkgrel=1
pkgdesc="Sphinx API for Web Apps"
arch=('any')
url="http://www.sphinx-doc.org/"
license=('BSD')
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools')
source=(https://pypi.org/packages/source/s/sphinxcontrib-websupport/sphinxcontrib-websupport-$pkgver.tar.gz)
sha512sums=('67807b2505e063639163b020f0e62469b0c2838c303516cce9a8d90181cc7b8a10d76532adb1267556d40f95966efc20ec9f8ee1a6731056bda827a6f97f40ec')

prepare() {
  cp -a sphinxcontrib-websupport-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/sphinxcontrib-websupport-$pkgver
  python setup.py build

  cd "$srcdir"/sphinxcontrib-websupport-$pkgver-py2
  python2 setup.py build
}

package_python-sphinxcontrib-websupport() {
  cd "$srcdir"/sphinxcontrib-websupport-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-sphinxcontrib-websupport() {
  cd "$srcdir"/sphinxcontrib-websupport-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
