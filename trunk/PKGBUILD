# Maintainer: Alad Wenter <alad@archlinux.org>
# Contributor: ibrokemypie <ibrokemypie@bastardi.net>
pkgname=i3lock-color
pkgver=2.12.c.4
pkgrel=1
_commit=720e2b4cfce703b870788de283be21e50c22096f
pkgdesc="improved screenlocker with color configuration support"
arch=('x86_64')
url=https://github.com/Raymo111/i3lock-color/
license=('MIT')
depends=('xcb-util-image' 'pam' 'libev' 'cairo' 'libxkbcommon-x11'
         'libjpeg-turbo' 'xcb-util-xrm')
makedepends=('git')
provides=('i3lock')
conflicts=('i3lock')
source=("$pkgname-$pkgver::git+https://github.com/PandorasFox/$pkgname#commit=$_commit")
sha256sums=('SKIP')

prepare() {
    cd "$pkgname-$pkgver"
    # Add XF86Sleep to --pass-power-keys
    git cherry-pick --no-commit dbb21b5341307f682533d517c9cc61e4b6cc6cf6

	# Fix ticket FS#31544, sed line taken from gentoo
	sed -i -e 's:login:system-auth:' pam/i3lock 
    autoreconf -fiv
}

build() {
    cd "$pkgname-$pkgver"
    ./configure --prefix=/usr --sysconfdir=/etc
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
