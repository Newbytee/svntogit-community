# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>
# Contributor: larte <lauri.arte@gmail.com>
# Contributor: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: gun1x <gunix@gunix.cloud>

pkgname=kubectl
pkgdesc="Kubernetes.io client binary"
pkgver=1.18.5
pkgrel=2
arch=('x86_64')
url="https://kubernetes.io"
license=('Apache')
depends=('glibc')
makedepends=('go' 'rsync' 'git')
#source=("git+https://github.com/kubernetes/kubernetes#tag=v${pkgver}")
source=("kubernetes-$pkgver.tar.gz::https://dl.k8s.io/v$pkgver/kubernetes-src.tar.gz"
        "gotags.patch")
sha512sums=('9ddcd8b517e3cf78113ef977c365d26f0f27fe9b7fd9410f0214fd38cd38c6de6f37d7fbe2bb398587caceb4ae484afc2fe7ae14b80b70fe728405f3d5bd4115'
            '413ddce3e475107db030a04d6ed53d145ead8d13d2d153c4cb6ce635643d7cc922cab90f528b7f8152b43f157c2c441759e6d48511be1a9a16e491e177bf97e8')

prepare(){
  patch -Np1 < "$srcdir/gotags.patch"
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  make WHAT=cmd/kubectl GOFLAGS="-buildmode=pie -trimpath"
  # we don't need this anymore, but let's keep it.
  #cd cmd/kubectl
  #go build -trimpath -ldflags "-extldflags ${LDFLAGS}" -o kubectl
}

package() {
  install -Dm755 _output/bin/kubectl -t "$pkgdir/usr/bin"
  # man page generation still seems to fail with 1.17.2 :(
  # install -Dm644 docs/man/man1/* -t "$pkgdir/usr/share/man/man1"

  "$pkgdir/usr/bin/kubectl" completion bash | install -Dm644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/kubectl"
  "$pkgdir/usr/bin/kubectl" completion zsh | install -Dm644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_kubectl"
}
