# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: d'Ronin <daronin@2600.com>
# Contributor: Hexchain Tong <richard0053@gmail.com>
# Contributor: Jack Lloyd <jack@randombit.net>

pkgname=botan
pkgver=2.5.0
pkgrel=3
pkgdesc='Crypto library written in C++'
arch=('x86_64')
url='https://botan.randombit.net/'
license=('BSD')
makedepends=('git' 'python')
# Using a specific commit instead of the .tgz because botan issue #1524
source=('git+https://github.com/randombit/botan#commit=6c5d9ef3c49658cbf4096e04905e3c0debb9d096')
md5sums=('SKIP')
#validpgpkeys=('621DAF6411E1851C4CF9A2E16211EBF1EFBADFBC')
#source=("https://botan.randombit.net/releases/Botan-${pkgver}.tgz"{,.asc})
#sha256sums=('b8a31fe03e7f048a5bd3967ecd04b6a48966215e78792df06e333b0eede4fb1b'
#            'SKIP')

build() {
  cd "$pkgname"
  #cd "${pkgname^}-$pkgver"

  ./configure.py --prefix=/usr
  make
}

check() {
  cd "$pkgname"

  ./botan-test
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install
  install -Dm644 license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/randombit/botan
# vim: ts=2 sw=2 et:
