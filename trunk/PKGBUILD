# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-gilt
pkgver=1.2.1
pkgrel=3
pkgdesc="A GIT layering tool"
arch=('any')
url="https://github.com/retr0h/gilt"
license=('MIT')
depends=('python-click' 'python-colorama' 'python-fasteners'
'python-git-url-parse' 'python-pbr' 'python-yaml' 'python-sh')
makedepends=('python-setuptools')
checkdepends=('git' 'python-pytest' 'python-pytest-cov'
'python-pytest-helpers-namespace' 'python-pytest-mock' 'python-requests'
'yapf')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-1.2.1-ls_path.patch"
        "${pkgname}-1.2.1-unittests.patch::https://github.com/retr0h/gilt/pull/67.patch")
sha512sums=('d5278365428623b7c172d981a437c6a6e318bf4e84844e7978d7821480c93edaa1e8a1dd6920bc5b7d33d26b5475ad5dcb8f37baea5586fe53c5397d320b2e69'
            '9e2521de91ef96f86a1a7ca35ada1d80cc384e9f639721f0cffdc1d66d78803f5a1fcb3fab7f98450a43cbedcbc91ef34c67be0d89feaf1f40b2f31fac324738'
            '9d9698086ec27b23b9db2b7ba424d64c5b2d52e407b7fe9de8aa9a29671b51b90340b34c33b246b1fe7c1d054c4a1f88ab0740abf4e8eb1ddfc5d1bd0e37b1d9'
            '5ad0d9149961b870146f7aa8e1ca2ce00d1dd65b80f93d2458265d106f5d2a6bb8249ace55191977946eeb8d789777f21f1be6b02c19471ee62fb1a7fce54866')

prepare() {
  cd "$pkgname-$pkgver"
  # applying unreleased fix for problems with unit tests:
  # https://github.com/retr0h/gilt/issues/66
  patch -Np1 -i "../${pkgname}-1.2.1-unittests.patch"
  # fixing issues with hardcoded paths
  # https://github.com/retr0h/gilt/issues/76
  patch -Np1 -i "../${pkgname}-1.2.1-ls_path.patch"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {AUTHORS,CHANGELOG,CONTRIBUTING,README}.rst -t \
    "${pkgdir}/usr/share/doc/${pkgname}"
}
