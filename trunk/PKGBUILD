# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=python2-sphinx
pkgver=1.1.3
pkgrel=1
pkgdesc='Python2 documentation generator'
arch=('any')
url='http://sphinx.pocoo.org/'
license=('GPL')
makedepends=('python2-distribute')
checkdepends=('python2-nose' 'texlive-latexextra')
depends=('python2-jinja' 'python2-pygments' 'docutils')
optdepends=('texlive-latexextra: for generation of PDF documentation')
conflicts=('python-sphinx')
source=("http://pypi.python.org/packages/source/S/Sphinx/Sphinx-$pkgver.tar.gz")
md5sums=('8f55a6d4f87fc6d528120c5d1f983e98')
 
build() {
  cd Sphinx-$pkgver
  # fix python sheebang
  find . -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
  make PYTHON=python2 build
}

check() {
  cd Sphinx-$pkgver
  make PYTHON=python2 test
}
 
package() {
  cd Sphinx-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 ft=sh et:
