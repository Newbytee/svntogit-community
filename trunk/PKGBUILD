# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Christoph Siegenthaler <csi@gmx.ch>
# Contributor: Jesse Young <jesse@eon.jlyo.org>

pkgname=sauerbraten-data
pkgver=2010_07_28_justice_edition
pkgrel=3
pkgdesc='Improved version of the Cube engine - datas files'
arch=('any')
url="http://www.sauerbraten.org/"
license=('ZLIB')
makedepends=('sdl_mixer' 'sdl_image' 'libgl' 'mesa')
source=(http://downloads.sourceforge.net/sauerbraten/sauerbraten_${pkgver}_linux.tar.bz2
	license.txt
	fix-indirect-linking.diff)
md5sums=('0947cb37b628873063270be1fcce27f6'
         'cf46cd0148f03b7477f7c54aec865abe'
         'a9a9b658affd933ad354be45da998354')

build() {
  cd ${srcdir}/sauerbraten/src

  patch -Np1 -i ../../fix-indirect-linking.diff

  make

  # remove cvs directories
  find ${srcdir}/sauerbraten -type d -name CVS -exec rm -rf '{}' \; 2> /dev/null
}

package() {
  cd ${srcdir}/sauerbraten

  install -d ${pkgdir}/opt/sauerbraten
  cp -r data ${pkgdir}/opt/sauerbraten
  cp -r packages ${pkgdir}/opt/sauerbraten

  # make namcap happy
  chmod -R go+rx ${pkgdir}/opt/sauerbraten/packages/argh

  install -Dm644 ${srcdir}/license.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
