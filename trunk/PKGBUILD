# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: gardar <aur@gardar.net>
# Contributor: Morten Linderud <morten@linderud.pw>

pkgbase="python-testinfra"
pkgname="python-testinfra"
_pkgname="testinfra"
pkgver=5.0.0
pkgrel=1
pkgdesc='Testinfra test your infrastructures'
url="https://github.com/philpep/testinfra"
arch=('any')
license=('Apache')
depends=('python-six')
makedepends=('python' 'python-setuptools' 'python-pbr' 'python-sphinx')
#checkdepends=('python-mock' 'python-pytest-cov' 'python-pytest-xdist'
#'python-paramiko' 'python-tornado' 'salt' 'ansible' 'python-pywinrm')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/philpep/testinfra/archive/${pkgver}.tar.gz")
sha512sums=('915d4819b3d289b9b87dba614e2d5d9ca48e80c1e7dd0df60d64fad31f27c19b7914179422547c8eaab2e203f8d4a460972898599bc439348a6fb7cbb357991d')



build() {
    cd "${_pkgname}-${pkgver}"
    SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}" python setup.py build
    make -C doc html man
}

# This checks are disabled, because salt, doesn't support python3 yet.
# See also: https://bugs.archlinux.org/task/61129
#check() {
#    cd "${_pkgname}-${pkgver}"
#    SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}" python setup.py test
#}

package() {
    cd "${_pkgname}-${pkgver}"
    SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}" python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -d "${pkgdir}/usr/share/doc/${pkgname}"
    cp -r doc/build/html/* "${pkgdir}/usr/share/doc/${pkgname}"
    install -Dm 644 doc/build/man/testinfra.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}
